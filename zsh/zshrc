# {{{ Lines configured by zsh-newuser-install
HISTFILE=~/.histfile
HISTSIZE=1000000
SAVEHIST=1000000
zstyle ':completion::complete:*' use-cache 1

setopt SHARE_HISTORY HIST_IGNORE_ALL_DUPS HIST_IGNORE_SPACE
setopt NUMERIC_GLOB_SORT
setopt autocd extendedglob notify correct

bindkey -v
# End of lines configured by zsh-newuser-install
# The following lines were added by compinstall

zstyle ':completion:*' completer _expand _complete _ignored _approximate
zstyle ':completion:*' matcher-list '' 'm:{[:lower:]}={[:upper:]}' 'm:{[:lower:][:upper:]}={[:upper:][:lower:]}' 'l:|=* r:|=*'

autoload -Uz compinit
compinit
# }}} End of lines added by compinstall

### My Own custom modification

# SOURCES {{{

#STUFF=$HOME/linux
source ~/.linux_stuff

source $STUFF/shell/environment
source $STUFF/shell/functions
source $STUFF/shell/aliases

source $STUFF/zsh/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh
source $STUFF/zsh/zsh-history-substring-search/zsh-history-substring-search.zsh
source $STUFF/zsh/git-prompt/zshrc.sh

# }}}

# Launching TMUX fi console {{{
if [ -n "$IM_CONSOLE" ] ; then

  # Launching tmux
  if [ -z "$TMUX" ]; then
    tmux
  fi

fi
#}}}

# Completion {{{
#zstyle -e ':completion::*:*:*:hosts' hosts 'reply=(${=${${(f)"$(cat {/etc/ssh_,~/.ssh/known_}hosts(|2)(N) /dev/null)"}%%[# ]*}//,/ })'
fpath=($STUFF/zsh/zsh-completions/src $fpath)
# }}}


# KEYBINDING {{{
# create a zkbd compatible hash;
# to add other keys to this hash, see: man 5 terminfo
zmodload zsh/terminfo
typeset -A key

key[Home]=${terminfo[khome]}
key[End]=${terminfo[kend]}
key[Insert]=${terminfo[kich1]}
key[Delete]=${terminfo[kdch1]}
key[Up]=${terminfo[kcuu1]}
key[Down]=${terminfo[kcud1]}
key[Left]=${terminfo[kcub1]}
key[Right]=${terminfo[kcuf1]}
key[PageUp]=${terminfo[kpp]}
key[PageDown]=${terminfo[knp]}

# setup key accordingly
[[ -n "${key[Home]}"    ]]  && bindkey  "${key[Home]}"    beginning-of-line
[[ -n "${key[End]}"     ]]  && bindkey  "${key[End]}"     end-of-line
[[ -n "${key[Delete]}"  ]]  && bindkey  "${key[Delete]}"  delete-char
[[ -n "${key[Up]}"      ]]  && bindkey  "${key[Up]}"      history-substring-search-up
[[ -n "${key[Down]}"    ]]  && bindkey  "${key[Down]}"    history-substring-search-down
[[ -n "${key[Left]}"    ]]  && bindkey  "${key[Left]}"    backward-char
[[ -n "${key[Right]}"   ]]  && bindkey  "${key[Right]}"   forward-char

# Finally, make sure the terminal is in application mode, when zle is
# active. Only then are the values from $terminfo valid.
if (( ${+terminfo[smkx]} )) && (( ${+terminfo[rmkx]} )); then
    function zle-line-init () {
        printf '%s' "${terminfo[smkx]}"
    }
    function zle-line-finish () {
        printf '%s' "${terminfo[rmkx]}"
    }
    zle -N zle-line-init
    zle -N zle-line-finish
fi

# bind P and N for EMACS mode
bindkey -M emacs '^P' history-substring-search-up
bindkey -M emacs '^N' history-substring-search-down

# bind k and j for VI mode
bindkey -M vicmd 'k' history-substring-search-up
bindkey -M vicmd 'j' history-substring-search-down

# Upper K and J for normal history search
bindkey -a 'K' up-line-or-history
bindkey -a 'J' down-line-or-history

# }}}

# PROMT {{{

export __GIT_PROMPT_DIR=$STUFF/zsh/git-prompt

autoload -U promptinit
promptinit
prompt walters
setopt PROMPT_SUBST
#PROMPT='%(!.%F{red}.%F{yellow})%1~ %f%(!.%F{red}.%F{blue})%#%f '
#PROMPT='$(git_super_status) %f%(!.%F{red}.%F{yellow})∞%f '
if [ -z "$IM_SSH" ] ; then
  PROMPT='$(git_super_status) %f%(!.%F{red}.%F{yellow})Ӄ%f '
else
  PROMPT='$(git_super_status) %F{green}%n%F{yellow}@%F{red}%m %f%(!.%F{red}.%F{yellow})>%f '
fi

# }}}

# CD HOOK {{{
chpwd_functions=(save_dir)
# }}}


# TERMINAL TITLE {{{

termtitle() {
  local prompt_host="${(%):-%m}"
  local prompt_user="${(%):-%n}"
  local prompt_char="${(%):-%  ҉}"
  case "$1" in
    precmd)
      local str="${prompt_user[1,2]} $prompt_char"
      ;;
    preexec)
      local str="${prompt_user[1,2]} $prompt_char $2"
      ;;
  esac
  case "$TERM" in
    rxvt*|xterm|nxterm|gnome)
      #print -Pn "\ek%-3 $str\e\\" # set screen title.  Fix vim: ".
      print -Pn "\e]2;%-3 $str\a" # set xterm title, via screen "Operating System Command"
      ;;
    screen.*|screen|screen-*)
      print -Pn "\e]2;%m:%-3 $str\a"
      ;;
  esac
}

precmd() {
  # Set terminal title.
  termtitle precmd
}

preexec() {
  # Set terminal title along with current executed command pass as second argument
  termtitle preexec "${(V)1}"
}

# }}}
